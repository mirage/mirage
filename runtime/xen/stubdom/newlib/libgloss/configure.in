dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT([libgloss],[LIBGLOSS_VERSION])
AC_CONFIG_SRCDIR([libnosys])

if test "${enable_shared}" = "yes" ; then
    echo "Shared libraries not supported for cross compiling, ignored"
fi

if test "${srcdir}" = "." ; then
  if test "${with_target_subdir}" != "." ; then
    libgloss_topdir="${srcdir}/${with_multisrctop}../.."
  else
    libgloss_topdir="${srcdir}/${with_multisrctop}.."
  fi
else
  libgloss_topdir="${srcdir}/.."
fi
AC_CONFIG_AUX_DIR($libgloss_topdir)

AC_PROG_INSTALL

AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE([cygnus no-define 1.9.5])

if test -z "${with_multisubdir}" ; then
  AC_CONFIG_SUBDIRS(doc)
fi

AC_CONFIG_SUBDIRS(libnosys)

case "${target}" in
  i[[3456]]86-*-elf* | i[[3456]]86-*-coff*)
	AC_CONFIG_SUBDIRS(i386)
	config_testsuite=true
	;;
  m32r-*-*)
	AC_CONFIG_SUBDIRS(m32r)
	config_testsuite=true
	;;
  m68hc11-*-* | m6811-*-* | m68hc12-*-* | m6812-*-*)
	AC_CONFIG_SUBDIRS(m68hc11)
	config_testsuite=true
	;;
  fido-*-* | m68*-*-*)
	AC_CONFIG_SUBDIRS(m68k)
	config_testsuite=true
	;;
  hppa*-*-pro*)
	AC_CONFIG_SUBDIRS(pa)
	config_testsuite=true
	;;
  i960-*-coff)
	AC_CONFIG_SUBDIRS(i960)
	config_testsuite=true
	;;
  sparclet-*-aout* | sparc-*-elf* | sparc64-*-elf* | sparc86x-*-* | sparclite-*-*)
	AC_CONFIG_SUBDIRS(sparc)
	config_testsuite=true
	;;
  sh*-*-pe | mips*-*-pe | *arm-wince-pe)
	AC_CONFIG_SUBDIRS(wince)
	;;
  mips*-*-*)
	AC_CONFIG_SUBDIRS(mips)
	config_testsuite=true
	;;
  powerpc-*-*|powerpcle-*-*)
	AC_CONFIG_SUBDIRS(rs6000)
	config_testsuite=true
	;;
  mn10200-*-*)
	AC_CONFIG_SUBDIRS(mn10200)
	config_testsuite=true
	;;
  mn10300-*-*)
	AC_CONFIG_SUBDIRS(mn10300)
	config_testsuite=true
	;;
  bfin-*-*)
	AC_CONFIG_SUBDIRS(bfin)
	config_testsuite=true
	;;
  cris-*-* | crisv32-*-*)
	AC_CONFIG_SUBDIRS(cris)
	config_testsuite=true
	;;
  crx-*-*)
	AC_CONFIG_SUBDIRS(crx)
	;;
  d30v-*-*)
	AC_CONFIG_SUBDIRS(d30v)
	config_testsuite=true
	;;
  fr30-*-*)
	AC_CONFIG_SUBDIRS(fr30)
	config_testsuite=true
	;;
  frv*-*-*)
	AC_CONFIG_SUBDIRS(frv)
	config_testsuite=true
	;;
  mcore-*-*)
	AC_CONFIG_SUBDIRS(mcore)
	config_testsuite=true
	;;
  mep-*-*)
	AC_CONFIG_SUBDIRS(mep)
	config_testsuite = true;
	;;
  mt-*-*)
        AC_CONFIG_SUBDIRS(mt)
	config_testsuite=true
	;;
  xstormy16-*-*)
	AC_CONFIG_SUBDIRS(xstormy16)
	config_testsuite=true
	;;
  m32c-*-*)
	AC_CONFIG_SUBDIRS(m32c)
	config_testsuite=true
	;;
  strongarm-*-elf | strongarm-*-coff | arm*-*-elf | arm*-*-coff | ep9312-*-elf | ep9312-*-coff | xscale-*-elf | xscale-*-coff | *arm-*-*)
	AC_CONFIG_SUBDIRS(arm)
	config_testsuite=true
	;;
  spu-*-elf)
	AC_CONFIG_SUBDIRS(spu)
	config_testsuite=false
	;;
  iq2000-*-*)
	AC_CONFIG_SUBDIRS(iq2000)
	config_testsuite=true
	;;
esac

# For now, don't bother configuring testsuite
#
# if test ${config_testsuite); then AC_CONFIG_SUBDIRS(testsuite); fi
#

LIB_AC_PROG_CC
AS=${AS-as}
AC_SUBST(AS)
AC_CHECK_PROG(AR, ar, ar, :)
AR=${AR-ar}
AC_SUBST(AR)
LD=${LD-ld}
AC_SUBST(LD)
AC_PROG_RANLIB
LIB_AM_PROG_AS

host_makefile_frag=${srcdir}/config/default.mh

dnl We have to assign the same value to other variables because autoconf
dnl doesn't provide a mechanism to substitute a replacement keyword with
dnl arbitrary data or pathnames.
dnl
host_makefile_frag_path=$host_makefile_frag
AC_SUBST(host_makefile_frag_path)
AC_SUBST_FILE(host_makefile_frag)

if test "${multilib}" = "yes"; then
  multilib_arg="--enable-multilib"
else
  multilib_arg=
fi

# for now, only add multilibs for specific targets
AC_CONFIG_FILES([Makefile],
[if test -n "$CONFIG_FILES"; then
  unset ac_file
  . ${libgloss_topdir}/config-ml.in
fi],
srcdir=${srcdir}
target=${target}
with_multisubdir=${with_multisubdir}
ac_configure_args="--enable-multilib ${ac_configure_args}"
CONFIG_SHELL=${CONFIG_SHELL-/bin/sh}
libgloss_topdir=${libgloss_topdir}
CC="${CC}"
LDFLAGS=${LDFLAGS}
)
AC_OUTPUT


